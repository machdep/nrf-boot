MEMORY
{
	flash   (rx)  : ORIGIN = 0x00000000, LENGTH = 1M
	sram0   (rwx) : ORIGIN = 0x20000000, LENGTH = 64K /* this app */
	sram1   (rwx) : ORIGIN = 0x20010000, LENGTH = 64K /* bsdlib */
	sram2   (rwx) : ORIGIN = 0x20020000, LENGTH = 64K /* main app */
	sram3   (rwx) : ORIGIN = 0x20030000, LENGTH = 64K /* malloc */
}

ENTRY(app_main)
SECTIONS
{
	. = 0x0;
	.start . : {
		*start.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	/* Ensure _smem is associated with the next section */
	. = .;
	PROVIDE(_smem = .);
	.data : {
		PROVIDE(_sdata = .);
		*(.data*);
		PROVIDE(_edata = .);
	} > sram0 AT > flash

	.bss : {
		PROVIDE(_sbss = .);
		*(.bss*)
		*(COMMON)
		PROVIDE(_ebss = .);
	} > sram0

	. = ALIGN(4);
	. = . + 0x1000;
	stack_top = .;
}
