MEMORY
{
	flash   (rx)  : ORIGIN = 0x00000000, LENGTH = 1M
	sram0   (rwx) : ORIGIN = 0x20000000, LENGTH = 64K
	sram1   (rwx) : ORIGIN = 0x20010000, LENGTH = 64K /* bsdlib */
	sram2   (rwx) : ORIGIN = 0x20020000, LENGTH = 64K
	sram3   (rwx) : ORIGIN = 0x20030000, LENGTH = 64K /* malloc */
}

ENTRY(app_main)
SECTIONS
{
	. = 0x0;
	.start . : {
		*start.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	_smem = ABSOLUTE(.);
	.data : {
		_sdata = ABSOLUTE(.);
		*(.data*);
		_edata = ABSOLUTE(.);
	} > sram0 AT > flash

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss*)
		*(COMMON)
		_ebss = ABSOLUTE(.);
	} > sram0

	. = ALIGN(4);
	. = . + 0x1000;
	stack_top = .;
}
